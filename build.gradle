plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java'
    id 'application'
    id 'jacoco'
    id "com.github.ben-manes.versions" version "0.28.0"

}

sourceCompatibility = 14
targetCompatibility = 14

jacocoTestReport {
    group 'verification'
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

mainClassName = "org.me.JavaApplicationMeOrg"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "joda-time:joda-time:2.10.5"
    testImplementation group: 'junit', name: 'junit', version: '4.+'
}


jar {
    manifest {
        attributes 'Main-Class': 'org.me.JavaApplicationMeOrg'
    }
}

shadowJar {
    destinationDirectory=file("$buildDir/shadowJar")
}

task hello {
    doLast {
        println 'Hello world!'
    }
}

wrapper {
    gradleVersion = '6.3-rc-4' //version required
}

ext.execPath = "$System.env.PATH" + System.getProperty("path.separator") + "c:/program files/wix"

task jpackage(type: Exec, dependsOn: 'shadowJar') {
    environment "PATH", execPath
    commandLine System.properties['java.home'] + "/bin/jpackage",
            '--name', 'Animal',
            '--app-version', '1.1.2',
            '--input', "$buildDir/shadowJar",
            '--main-jar', "JavaTemplateApplication-all.jar",
            '--dest', "$buildDir/jdepend"
    environment << [JPACKAGE_DEBUG: true]
}
